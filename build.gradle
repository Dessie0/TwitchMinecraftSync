plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'java-library'
    id 'maven-publish'
}

group 'com.twitchmcsync'
version '1.2.4'

java.toolchain {
    languageVersion = JavaLanguageVersion.of(8)
}

compileJava.options.encoding = 'UTF-8'

task copyJar(type: Copy) {
    from "$buildDir/libs/"
    into project.findProperty("outputDir")
}

build.finalizedBy copyJar

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url 'https://repo.opencollab.dev/maven-snapshots/'
    }

    maven {
        url 'https://jitpack.io'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dessie0/twitchminecraftsync")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId 'com.twitchmcsync'
            artifactId 'twitchminecraftsync'
            from(components.java)
        }
    }
}

dependencies {
    //compileOnly "org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'org.geysermc.floodgate:api:2.0-SNAPSHOT'
    implementation 'net.lingala.zip4j:zip4j:2.11.0'
}

//Used to create the fat jar
build.dependsOn {
    shadowJar {
        archiveFileName = "${project.name}-${project.version}.jar"
    }
}